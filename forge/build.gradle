plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    forge()
}


loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name

        mixinConfig  "jeed-common.mixins.json"
        mixinConfig  "jeed.mixins.json"
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}


processResources {
    inputs.property "version", project.version
    inputs.property "mod_name", project.getArchivesBaseName()

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version,
                "mod_name": project.getArchivesBaseName()
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null
}

jar {
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.mod_id + "-" + project.name
            from components.java
        }
    }

    repositories {
        maven {
            url = "https://www.cursemaven.com"
            allowInsecureProtocol = true
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories{
    flatDir {
        dirs "mods"
    }
    mavenCentral()
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

    implementation 'org.jetbrains:annotations:22.0.0'
    modImplementation   ("curse.maven:jei-238222:4597711")

    modCompileOnly "me.shedaniel:RoughlyEnoughItems-forge:12.0.625"

    //modRuntimeOnly("curse.maven:roughly-enough-items-hacks-521393:4402999")
    modRuntimeOnly("curse.maven:architectury-api-419699:4581905")
    modCompileOnly("curse.maven:stylish-effects-543661:4582073")
    modCompileOnly("curse.maven:puzzles-lib-495476:4596893")
    //modImplementation ("curse.maven:jei-238222:3884337")
    //modImplementation ("curse.maven:configured-457570:4011355")
}
